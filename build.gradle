buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

minecraft {
	version = minecraft_version + "-" + forge_version
	mappings = mappings_version
	runDir = "run"
}

group = project.projectDir.name.toLowerCase()
def modId = project.projectDir.name
archivesBaseName = modId + "-mc" + project.minecraft.version

if (version == "unspecified")
	version = ""
if (System.getenv("BUILD_NUMBER") != null)
	version += (version ? "+" : "") + "${System.getenv("BUILD_NUMBER")}"
if (System.getenv("GIT_COMMIT") != null)
	version += (version ? "." : "") + "${System.getenv("GIT_COMMIT").take(5)}"

sourceSets.main{
	java {
		srcDirs 'java', 'apis'
	}
	resources {
		srcDirs 'resources'
	}
}

dependencies {
	compile fileTree(dir: 'libs', include: '*.jar,*.zip')
}

jar {
	manifest {
		attributes 'FMLCorePlugin': 'squeek.applecore.AppleCore'
		attributes 'FMLCorePluginContainsFMLMod': 'true'
	}
}

task deobfArtifact(type: Jar) {
	from sourceSets.main.output
	manifest = jar.manifest
	classifier = 'deobf'
}

task apiArtifact(type: Jar, dependsOn: compileJava) {
	from sourceSets.main.output
	include "squeek/applecore/api/**"
	classifier = 'api'
}

task sourcesArtifact(type: Jar, dependsOn: compileJava) {
	from project.buildDir.getPath()+"/sources/java"
	classifier = 'sources'
}

artifacts {
	archives deobfArtifact
	archives apiArtifact
	archives sourcesArtifact
}

processResources {
	inputs.property "vars", project.version + project.minecraft.version + api_version
	from(sourceSets.main.resources.srcDirs) {
		include '**/*.info'
		expand 'version':project.version, 'mcversion':project.minecraft.version, 'apiversion':api_version
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude '**/*.info'
	}
}

// variable substitution in java files
task processSourceMainJava(type:Copy) {
	inputs.property "vars", project.version + project.minecraft.version + api_version
	from('java')
	{
		include '**/ModInfo.java'
		include '**/package-info.java'
		expand 'version':project.version, 'mcversion':project.minecraft.version, 'apiversion':api_version
	}
	into project.buildDir.getPath()+"/sources/java"
}

task removeExampleAndReferenceSources(type:Delete) {
	delete += project.buildDir.getPath()+"/sources/java"+"/squeek/applecore/example"
	delete += project.buildDir.getPath()+"/sources/java"+"/squeek/applecore/asm/reference"
}

task removeExampleAndReferenceResources(type:Delete) {
	delete += file(sourceSets.main.output.resourcesDir.getPath()+"/mcmod.info")
}

// insert custom tasks
afterEvaluate { project ->
	sourceMainJava.finalizedBy(processSourceMainJava)
	if (!project.hasProperty('buildexample'))
	{
		sourceMainJava.finalizedBy(removeExampleAndReferenceSources)
		processResources.finalizedBy(removeExampleAndReferenceResources)
	}
}

ext.mavenUrl = project.hasProperty('mavenUrl') ? mavenUrl : System.getenv("MAVEN_URL")
ext.mavenUser = project.hasProperty('mavenUser') ? mavenUser : System.getenv("MAVEN_USER")
ext.mavenPass = project.hasProperty('mavenPass') ? mavenPass : System.getenv("MAVEN_PASS")

if (mavenUrl && !project.hasProperty('nopublish')) {
	apply plugin: 'maven'

	configurations {
		deployerJars
	}
	dependencies {
		deployerJars "org.apache.maven.wagon:wagon-webdav:1.0-beta-2"
	}
	uploadArchives {
		repositories.mavenDeployer {
			configuration = configurations.deployerJars
			pom.artifactId = modId
			pom.version = project.minecraft.version + "-" + project.version
			pom.project {
				url = "https://github.com/squeek502/" + modId
			}
			repository(url: mavenUrl) {
				authentication(userName: mavenUser, password: mavenPass)
			}
		}
	}
	afterEvaluate { project ->
		build.finalizedBy(uploadArchives)
	}
}